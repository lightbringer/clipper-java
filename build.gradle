allprojects {
	group = "de.lighti"
	version = "6.4.2"
}

description = 'ClipperJava is a 1:1 Java port of the Clipper project developed by Angus Johnson, which as an implementation of the algorithm proposed by Bala R. Vatti. Clipper has various boolean operations (Union, Difference, XOR, etc.) on arbitrary 2D polygons, e.g. calculate the area in which two polygons overlap. It comes with two Demo applications, one for the Console and one using a Swing-based GUI.'

subprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'

	java {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
		withSourcesJar()
		withJavadocJar()
	}

	jar {
		manifest {
			attributes 'Implementation-Title': project.name,
					   'Implementation-Version': project.version
		}
	}

	sourceSets {
		main {
			java {
				srcDirs 'src'
			}
		}
	}

	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:unchecked'
		options.deprecation = true
	}
	
	publishing {
		publications {
			ClipperJava(MavenPublication) {
			   from components.java
		   }
		}
	}
	
	javadoc {
		if(JavaVersion.current().isJava9Compatible()) {
			options.addBooleanOption('html5', true)
		}
	}
}

project(':Clipper') {
	description = 'Clipper is a library with various boolean operations (Union, Difference, XOR, etc.) on arbitrary 2D polygons, e.g. calculate the area in which two polygons overlap.'
}

project(':ClipperConsole') {
	description = 'ClipperConsole is a command line tool to read and process polygon files.'

	dependencies {
		implementation project(':Clipper')
	}
}

project(':ClipperGUI') {
	description = 'ClipperGUI is a swing-based application to visualize the various operations of the Clipper library.'

	dependencies {
		implementation project(':Clipper')
	}
}
